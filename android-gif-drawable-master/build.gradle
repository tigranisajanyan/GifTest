import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.library'
apply from: 'gradle-mvn-push.gradle'

String ndkDebug = isDebugBuild() ? "NDK_DEBUG=1" : "NDK_DEBUG=0"

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' //set .so files location to libs
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    task buildNative(type: Exec) {
        commandLine getNdkBuildCommand(),
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                ndkDebug
    }

    task cleanNative(type: Exec) {
        commandLine getNdkBuildCommand(),
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildConfigField 'String', 'NATIVE_LIBRARY_NAME', '"' + NATIVE_LIBRARY_NAME + '"'
    }
    buildTypes.debug.jniDebuggable isDebugBuild()

    if (shouldApplyFlavors()) {
        productFlavors {
            flavors.each() {
                String flavor = it
                "${it}" {
                    ndk {
                        abiFilter flavor
                    }
                }
            }
            fat
        }
    }
}
clean.dependsOn cleanNative

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn buildNative
}

allprojects {
    version = VERSION_NAME
    group = GROUP
    repositories {
        mavenCentral()
    }
    dependencies {
        provided 'com.android.support:support-annotations:22.2.1'
    }
}

afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
        }
    }
}

def shouldApplyFlavors() {
    def flavors = ['armeabi', 'armeabi-v7a', 'x86', 'mips', 'arm64-v8a', 'mips64', 'x86_64']
    boolean applyFlavors = false
    gradle.startParameter.taskNames.each() {
        String normalizedTaskName = it.toLowerCase(Locale.US)
        flavors.each() {
            applyFlavors |= normalizedTaskName.contains(it)
        }
    }
    applyFlavors
}

def isDebugBuild() {
    !gradle.startParameter.taskNames.contains("uploadArchives") && VERSION_NAME.contains("SNAPSHOT")
}

def getNdkBuildCommand() {
    def ndkDir = android.ndkDirectory
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return "$ndkDir/ndk-build.cmd"
    } else {
        return "$ndkDir/ndk-build"
    }
}
